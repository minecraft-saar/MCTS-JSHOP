plugins {
	id 'java'
	id 'application'
    // shadow builds jar with all dependencies
    id 'com.github.johnrengelman.shadow' version '5.2.0'

    // enables automatic version numbers from git tags
    // and provides additional gradle tasks to create releases (optional)
    id 'pl.allegro.tech.build.axion-release' version '1.10.1'

    id 'maven-publish'
}
group 'de.saar.minecraft'
description 'jshop'

configurations.all {
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01'
  }
}

// This defines releases as revisions
// that have a tag of the form "release-X.Y.Z"
scmVersion {
	// the git repository in in the parent directory
    repository {
        directory = project.rootProject.file('../')
    }
    tag {
        prefix = 'release'
    }
}
version = scmVersion.version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    //maven { url "http://akci.coli.uni-saarland.de/artifactory/external"
    //       allowInsecureProtocol = true }
    maven { url 'https://jitpack.io' }
    maven {url 'https://raw.github.com/coli-saar/alto/repository/'}
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/minecraft-saar/jshop")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
            password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
        }
    }
}

dependencies {
	implementation 'info.picocli:picocli:3.9.3'
	implementation group: 'com.github.minecraft-saar', name: 'minecraft-nlg', version: '290ce47c6f139bcc22925078edaa33b2588006e2'
    implementation group: 'com.github.minecraft-saar', name: 'weight-estimator', version: 'b2813b71152d2ad126f3fcdf404b4046e5b42176'
    implementation group: 'ai.djl.pytorch', name: 'pytorch-engine', version: '0.15.0'
    implementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    implementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    runtimeOnly group: 'ai.djl.pytorch', name: 'pytorch-native-auto', version: '1.9.1'
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'
    implementation group: 'org.apache.clerezza.ext', name: 'org.json.simple', version: '0.4'
    implementation group: 'org.javatuples', name: 'javatuples', version: '1.2'
}

mainClassName = 'umd.cs.shop.JSJshop'

//jar {
//    manifest {
//        attributes(
//                'Main-Class': 'umd.cs.shop.JSJshop'
//        )
//    }
//}

// Define two additional tasks that build a sources jar and a javadoc
// jar.  Both are published at the maven repository.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// define the github package registry as the maven repository we push our software to.
// this happens automatically thanks to the github actions settings.
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/minecraft-saar/jshop")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
